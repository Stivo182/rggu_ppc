#Область ПрограммныйИнтерфейс

// Данные штатного расписания
//
// Параметры:
//   ПараметрыПолучения - см. ПараметрыПолученияШтатногоРасписания
//
// Возвращаемое значение:
//   ТаблицаЗначений - данные штатного расписания:
//       * Подразделение - СправочникСсылка.Подразделения
//       * Сотрудник - СправочникСсылка.ФизическиеЛица
//       * Должность - СправочникСсылка.Должности
//       * ВидЗанятости - ПеречислениеСсылка.ВидыЗанятости
//       * КоличествоСтавок - Число
//       * ОперацииЗавершены - Булево
//       * УченоеЗвание - СправочникСсылка.УченыеЗвания
//       * УченаяСтепень - СправочникСсылка.УченыеСтепени
//
Функция КадрвыеДанныеСотрудников(ПараметрыПолучения) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	СоздатьВТКадровыеДанныеСотрудников(ПараметрыПолучения, Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	КадровыеДанныеСотрудников.Подразделение,
	|	КадровыеДанныеСотрудников.Должность,
	|	КадровыеДанныеСотрудников.ВидЗанятости,
	|	КадровыеДанныеСотрудников.КоличествоСтавок,
	|	КадровыеДанныеСотрудников.ОперацииЗавершены,
	|	КадровыеДанныеСотрудников.УченоеЗвание,
	|	КадровыеДанныеСотрудников.УченаяСтепень
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Конструктор структуры для параметра ПараметрыПолучения функции Кадры.СоздатьВТКадровыеДанныеСотрудников.
//
// Возвращаемое значение:
//   Структура:
//       * Дата - Дата
//       * Подразделение - СправочникСсылка.Подразделения
//       * Сотрудники - Массив из СправочникСсылка.ФизическиеЛица
//       * ТолькоРаботающие - Булево
//
Функция ПараметрыПолученияКадовыхДанныхСотрудников() Экспорт
	
	Параметры = Новый Структура;	
	Параметры.Вставить("Дата", Дата(1, 1, 1));
	Параметры.Вставить("Подразделение", Справочники.Подразделения.ПустаяСсылка());
	Параметры.Вставить("Сотрудники", Новый Массив);
	Параметры.Вставить("ТолькоРаботающие", Ложь);

	Возврат Параметры;

КонецФункции
	
// Формирует временную таблицу ВТКадровыеДанныеСотрудников
//
// Параметры:
//		ПараметрыПолучения - см. ПараметрыПолученияКадовыхДанныхСотрудников
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
Процедура СоздатьВТКадровыеДанныеСотрудников(ПараметрыПолучения, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Дата", КонецДня(ПараметрыПолучения.Дата));
	Запрос.УстановитьПараметр("Подразделение", ПараметрыПолучения.Подразделение);
	Запрос.УстановитьПараметр("ЕстьПодразделение", ЗначениеЗаполнено(ПараметрыПолучения.Подразделение));
	Запрос.УстановитьПараметр("Сотрудники", ПараметрыПолучения.Сотрудники);
	Запрос.УстановитьПараметр("ЕстьСотрудники", ПараметрыПолучения.Сотрудники.Количество() > 0);
	Запрос.УстановитьПараметр("ТолькоРаботающие", ПараметрыПолучения.ТолькоРаботающие);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник,
	|	КадроваяИсторияСотрудниковСрезПоследних.Подразделение,
	|	КадроваяИсторияСотрудниковСрезПоследних.Должность,
	|	КадроваяИсторияСотрудниковСрезПоследних.ВидЗанятости,
	|	КадроваяИсторияСотрудниковСрезПоследних.КоличествоСтавок,
	|	КадроваяИсторияСотрудниковСрезПоследних.ОперацииЗавершены,
	|	ЕСТЬNULL(УченыеЗванияФизическихЛиц.УченоеЗвание, ЗНАЧЕНИЕ(Справочник.УченыеЗвания.ПустаяСсылка)) КАК УченоеЗвание,
	|	ЕСТЬNULL(УченыеСтепениФизическихЛиц.УченаяСтепень, ЗНАЧЕНИЕ(Справочник.УченыеСтепени.ПустаяСсылка)) КАК УченаяСтепень
	|ПОМЕСТИТЬ ВТКадровыеДанныеСотрудников
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&Дата,) КАК КадроваяИсторияСотрудниковСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УченыеСтепениФизическихЛиц КАК УченыеСтепениФизическихЛиц
	|		ПО КадроваяИсторияСотрудниковСрезПоследних.Сотрудник = УченыеСтепениФизическихЛиц.ФизическоеЛицо
	|		И УченыеСтепениФизическихЛиц.НомерПоПорядку = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УченыеЗванияФизическихЛиц КАК УченыеЗванияФизическихЛиц
	|		ПО КадроваяИсторияСотрудниковСрезПоследних.Сотрудник = УченыеЗванияФизическихЛиц.ФизическоеЛицо
	|		И УченыеЗванияФизическихЛиц.НомерПоПорядку = 1
	|ГДЕ
	|	(Сотрудник В (&Сотрудники)
	|	Или Не &ЕстьСотрудники)
	|	И (Подразделение = &Подразделение
	|	Или Не &ЕстьПодразделение)
	|	И (Не КадроваяИсторияСотрудниковСрезПоследних.ОперацииЗавершены
	|	Или Не &ТолькоРаботающие)";	
	
	Запрос.Выполнить();
		
КонецПроцедуры

#КонецОбласти