#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДатаНачала > ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьПользователю("Дата окончания учебного года должна быть больше даты начала",,,, Отказ);		
	КонецЕсли;
	
	ПроверитьНаПересекающийПериод(Отказ);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаПересекающийПериод(Отказ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	Справочник.УчебныеГоды КАК УчебныеГоды
	|ГДЕ
	|	Не УчебныеГоды.Ссылка = &Ссылка
	|	И УчебныеГоды.ДатаНачала <= &ДатаОкончания
	|	И УчебныеГоды.ДатаОкончания >= &ДатаНачала");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю("Временные границы пересекаются с другим учебным годом",,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли