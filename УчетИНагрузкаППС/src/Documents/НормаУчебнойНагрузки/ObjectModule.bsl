#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьДублиПодразделений();
	СформироватьПредставлениеПодразделений();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвиженияНормаУчебнойНагрузки();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияНормаУчебнойНагрузки()
	
	Движения.НормаУчебнойНагрузки.Записывать = Истина;
	
	Для Каждого СтрокаТаблицы Из Должности Цикл
		Если СтрокаТаблицы.КоличествоЧасов = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Подразделения.Количество() = 0 Тогда						
			Запись = Движения.НормаУчебнойНагрузки.Добавить();	
			Запись.Период = Дата;
			Запись.УчебныйГод = УчебныйГод;
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Иначе
			Для Каждого СтрокаПодразделение Из Подразделения Цикл
				Запись = Движения.НормаУчебнойНагрузки.Добавить();	
				Запись.Период = Дата;
				Запись.УчебныйГод = УчебныйГод;
				Запись.Подразделение = СтрокаПодразделение.Подразделение;
				ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоЧасовПрочее > 0 Тогда
		Если Подразделения.Количество() = 0 Тогда
			Запись = Движения.НормаУчебнойНагрузки.Добавить();	
			Запись.Период = Дата;
			Запись.УчебныйГод = УчебныйГод;
			Запись.КоличествоЧасов = КоличествоЧасовПрочее;	
		Иначе
			Для Каждого СтрокаПодразделение Из Подразделения Цикл
				Запись = Движения.НормаУчебнойНагрузки.Добавить();	
				Запись.Период = Дата;
				Запись.УчебныйГод = УчебныйГод;
				Запись.КоличествоЧасов = КоличествоЧасовПрочее;	
				Запись.Подразделение = СтрокаПодразделение.Подразделение;				
			КонецЦикла;		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьДублиПодразделений()	
	ТЗ = Подразделения.Выгрузить();
	ТЗ.Свернуть("Подразделение");
	Подразделения.Загрузить(ТЗ);
КонецПроцедуры

Процедура СформироватьПредставлениеПодразделений()
	
	ПодразделенияТекст = "";
	
	Если Подразделения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивПодразделений = Подразделения.Выгрузить().ВыгрузитьКолонку("Подразделение");
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Подразделения.Наименование
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|ГДЕ
	|	Подразделения.Ссылка В (&МассивПодразделений)");
	Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	
	МассивНаименований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");
	ПодразделенияТекст = СтрСоединить(МассивНаименований, ", ");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли