#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Ответственный = Пользователи.ТекущийПользователь(); 	
	КонецЕсли;
	
	ЭтоОбщаяНорма = Объект.Подразделения.Количество() = 0;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьФорму()
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Подразделения.ЗагрузитьЗначения(ТекущийОбъект.Подразделения.ВыгрузитьКолонку("Подразделение"));

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПоместитьПодразделенияВОбъект(ТекущийОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтоОбщаяНормаПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьФорму()
	
	Элементы.СтраницаПодразделения.Видимость = Не ЭтоОбщаяНорма;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьПодразделенияВОбъект(ТекущийОбъект)
		
	ТекущийОбъект.Подразделения.Очистить();
	
	Для Каждого Элемент Из Подразделения Цикл
		СтрокаТаблицы = ТекущийОбъект.Подразделения.Добавить();
		СтрокаТаблицы.Подразделение = Элемент.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти